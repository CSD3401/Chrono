#type vertex
#version 460 core

layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 a_Normal; // not used for this
layout(location = 2) in vec2 a_TexCoord;
layout(location = 5) in mat4 i_Model;
layout(location = 9) in vec3 i_IDRGB;

uniform mat4 u_View;
uniform mat4 u_Projection;

out vec2 v_TexCoord;
out vec3 v_ID;
out vec3 v_FragPos;

void main() {
	vec4 world = i_Model * vec4(aPos, 1.0);
	v_FragPos   = world.xyz;
	gl_Position = u_Projection * u_View * world;
    v_TexCoord = a_TexCoord;
	v_ID = i_IDRGB;
}

#type fragment
#version 460 core
#extension GL_ARB_bindless_texture : require

in vec2 v_TexCoord;
in vec3 v_ID;
in vec3 v_FragPos;

layout(location = 0) out vec4 FragColor;
layout(location = 1) out vec4 OutID;

// Base (albedo)
uniform vec3 	  u_BaseColor;
uniform sampler2D u_AlbedoMap;
uniform int 	  h_HasAlbedoMap;

// Opacity
uniform float     u_Opacity;       // scalar multiplier (default 1.0)
uniform sampler2D u_OpacityMap;
uniform int       h_HasOpacityMap;
uniform int   	  u_AlphaClip;     // 0 = off (opaque/blend), 1 = clip/discard
uniform float     u_AlphaCutoff;

// UV Transform
uniform vec3 	  u_Tiling = vec3(1.0, 1.0, 0.0);
uniform vec3 	  u_Offset = vec3(0.0, 0.0, 0.0);

uniform vec3   	  u_CameraPos;
uniform int    	  i_FogEnabled;   // 0/1
uniform vec3   	  i_FogColor;     // linear color!
uniform int    	  i_FogMode;      // 0=Linear, 1=Exp, 2=Exp2
uniform float  	  i_FogDensity;   // Exp/Exp2
uniform float  	  i_FogStart;     // Linear
uniform float  	  i_FogEnd;       // Linear

float FogFactor(float d){
    if (i_FogMode==0){ // Linear
        float denom = max(i_FogEnd - i_FogStart, 1e-4);
        return clamp((i_FogEnd - d) / denom, 0.0, 1.0);
    } else if (i_FogMode==1){ // Exp
        return clamp(exp(-i_FogDensity * d), 0.0, 1.0);
    } else { // Exp2
        float x = i_FogDensity * d;
        return clamp(exp(-x*x), 0.0, 1.0);
    }
}

void main() {
	// UV transform
	vec2 uv = v_TexCoord * u_Tiling.xy + u_Offset.xy;
	
	// Albedo
    vec3 albedo = u_BaseColor;
    if (h_HasAlbedoMap != 0) {
        albedo *= texture(u_AlbedoMap, uv).rgb;
    }

    // Opacity
    float opacity = clamp(u_Opacity, 0.0, 1.0);
    if (h_HasOpacityMap != 0) {
        opacity *= clamp(texture(u_OpacityMap, uv).r, 0.0, 1.0);
    }

    // Alpha clip
    if (u_AlphaClip != 0) {
        if (opacity < u_AlphaCutoff)
            discard;
    }
	
	// Fog
    vec3 color = albedo;
    if (i_FogEnabled == 1) {
        float d  = distance(u_CameraPos, v_FragPos);
        float ff = FogFactor(d);
        color    = mix(i_FogColor, color, ff);
    }

    FragColor = vec4(color, opacity);
    OutID     = vec4(v_ID, 1.0);
	OutID = vec4(v_ID, 1.0);
}
